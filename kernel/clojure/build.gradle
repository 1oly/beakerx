/*
 *  Copyright 2017 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

group 'clojure'

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def final kernelIdName = 'clojure'
def final pathToReplace = '__PATH__'
def final mainClassToReplace = '__MAIN_CLASS__'

mainClassName = 'com.twosigma.beakerx.clojure.kernel.Clojure'

configurations {
  provided
}

sourceSets {
  main {
    compileClasspath += configurations.provided
  }
  test {
    compileClasspath += configurations.provided
    runtimeClasspath += configurations.provided
  }
}

repositories {
  mavenCentral()
}

dependencies {
  provided project(':base')
  compile group: 'org.clojure', name: 'clojure', version: '1.8.0'
  compile group: 'com.cemerick', name: 'pomegranate', version: '0.3.1'

  testCompile project(':base').sourceSets.test.output
  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
  testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
  manifest {
    attributes "Main-Class": "$mainClassName"
  }
}

distZip.dependsOn installDist

task prepareJson(dependsOn: 'build') {
  doLast {
    println "Prepare clojure JSON for Jupyter notebook"
    println "Copy kernel.json to build/libs/json/ and replacing PATH with clojure.jar path"
    def text = createKernelJson(
            kernelIdName,
            kernelIdName + '/build/install/clojure/lib/',
            pathToReplace,
            mainClassToReplace,
            pathToBaseLibs(),
            "$mainClassName")
    def folder = file('build/libs/json')
    if (!folder.exists()) {
      println "Creating folder build/libs/json"
      folder.mkdirs()
    }

    file('build/libs/json/kernel.json').withWriter { w ->
      w << text
    }
  }
}

task kernelInstall(dependsOn: 'prepareJson') {
  doLast {
    println "Installing 'clojure' to Jupyter notebook"

    def hashStdOut = new ByteArrayOutputStream()
    def jsonFolder = file('build/libs/json/').getAbsolutePath();
    exec {
      commandLine "jupyter",
              "kernelspec",
              "install",
              "--sys-prefix",
              "--replace",
              "--name",
              kernelIdName,
              jsonFolder;
      standardOutput = hashStdOut
    }
    println hashStdOut.toString().trim();
    println "DONE"
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId 'com.twosigma'
      artifactId 'beaker-kernel-clojure'
      version '2.0-SNAPSHOT'

      from components.java
    }
  }
}

task staticContent(dependsOn: build) {
  doLast {
    cleanBeakerxStatic(kernelIdName)
    copyLibsToBeakerxStatic(kernelIdName, configurations, jar)
    saveBeakerxStaticKernelJson(kernelIdName, "$mainClassName", pathToReplace, mainClassToReplace)
  }
}
